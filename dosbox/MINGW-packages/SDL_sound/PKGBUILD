# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

_realname=SDL_sound
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.0.3
pkgrel=1
pkgdesc="A library to decode several popular sound file formats, such as .WAV and .MP3"
url="https://icculus.org/SDL_sound/"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=(LGPL)
depends=("${MINGW_PACKAGE_PREFIX}-SDL"
         "${MINGW_PACKAGE_PREFIX}-flac"
         "${MINGW_PACKAGE_PREFIX}-libmikmod"
         "${MINGW_PACKAGE_PREFIX}-libmodplug"
         "${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-physfs"
         "${MINGW_PACKAGE_PREFIX}-speex"
         "${MINGW_PACKAGE_PREFIX}-smpeg")
makedepends=("${MINGW_PACKAGE_PREFIX}-tools")
source=(https://icculus.org/SDL_sound/downloads/SDL_sound-$pkgver.tar.gz 
        timidity.patch
        flac.patch)
sha256sums=('3999fd0bbb485289a52be14b2f68b571cb84e380cc43387eadf778f64c79e6df'
            '9fe614784e12ce92f60f5202610842ed948570e448c72d34064ab75f4409f402'
            '5ffe81fe08453f4abaa2611f9646db6b8fad0dfeaaf8c98b94613d5c6295c691')

prepare() {
    cd SDL_sound-$pkgver
    patch -p0 -i ${srcdir}/timidity.patch
}

build() {
  cd SDL_sound-$pkgver

  CFLAGS+=" -I${MINGW_PREFIX}/include/smpeg -D__EXPORT__= -DSDL_SOUND_DLL_EXPORTS"
  CXXFLAGS+=" -I${MINGW_PREFIX}/include/smpeg -D__EXPORT__= -DSDL_SOUND_DLL_EXPORTS"

  ./configure --prefix=${MINGW_PREFIX} --disable-static
  make
}

package() {
  cd SDL_sound-$pkgver
  make DESTDIR="$pkgdir" install
  cc -mdll -o ${pkgdir}/${MINGW_PREFIX}/bin/SDL_sound.dll $(ls .libs/*.o) .libs/libSDL_sound.a \
    $(cat .libs/libSDL_sound.la | grep ^dependency_ | sed "s/'//g;s/.*=\ //")
  gendef - ${pkgdir}/${MINGW_PREFIX}/bin/SDL_sound.dll > dll.def
  dlltool -d dll.def -l ${pkgdir}/${MINGW_PREFIX}/lib/libSDL_sound.dll.a
  rm -f ${pkgdir}/${MINGW_PREFIX}/bin/*.exe
#  cc -o ${pkgdir}/${MINGW_PREFIX}/bin/playsound $(ls playsound/playsound-*.o) \
#    ${pkgdir}/${MINGW_PREFIX}/lib/libSDL_sound.dll.a \
#    $(cat .libs/libSDL_sound.la | grep ^dependency_ | sed "s/'//g;s/.*=\ //") -lphysfs
}
